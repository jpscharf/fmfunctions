/*
Purpose: Splits a string using the delimiter into a list.

Returns: List

Name: text.explode ( theDelimiter; theText )

Parameters: ( theDelimiter ) text
            ( theText ) text

Dependencies: NONE

References: Modeled after PHP's explode: http://us.php.net/manual/en/function.explode.php

NOTE: If the provided delimiter is a ¶ the function will return the original text. Also,
it will strip any existing ¶ as they would be ambiguous.

2012-09-12, JPS, Created.
2012-09-21, JPS, Re-wrote to use a global variable to increase the recursion limit.
2012-11-13, JPS, Changed the global variable ($$FUNCTION.EXPLODE.TEXT.RESULTS), to local variables ($FUNCTION.EXPLODE.TEXT.RESULTS).
2012-12-04, JPS, Fixed a MAJOR bug that caused the function to break if theText ended with theDelimiter
*/

Let (
    [
        //Check for the delimiter
        delimiterCount = PatternCount ( theText ; theDelimiter );

        //Get the delimiter length
        delimiterLength = Length ( theDelimiter );

        //Clean up theText
        theText = If ( theDelimiter <> ¶; Substitute ( theText; ¶; "" ));

        //Get the length of the text
        textLength = Length ( theText );

        //Get the location of the first delimiter
        delimiterPosition  = Position ( theText ; theDelimiter ; 1 ; 1 );

        //Get the first value and trim it
        firstValue = Trim ( Left ( theText ; delimiterPosition - 1 ));

        //Save the current value
        $FUNCTION.EXPLODE.TEXT.RESULTS = List ( $FUNCTION.EXPLODE.TEXT.RESULTS; firstValue );

        //Get the remaining text
        remainingText = Trim ( Right ( theText; textLength - delimiterPosition - delimiterLength + 1 ))

    ];

    Case (
        //If theText is empty return nothing
        IsEmpty ( theText );
            "";

        //If theDelimiter is empty return the original string
        IsEmpty ( theDelimiter ) or theDelimiter = ¶;
            theText;

        //If there is no delimiter return the results
        delimiterCount = 0 or delimiterPosition = textLength;
            Let (
                    [
                        theList = $FUNCTION.EXPLODE.TEXT.RESULTS;
                        //Clear the global
                        $FUNCTION.EXPLODE.TEXT.RESULTS = "";

                        //If the delimiter is the last character, remove it
                        theText = If ( delimiterPosition = textLength; Left ( theText; Length ( theText ) - delimiterLength - 1); theText)
                    ];
                    List ( theList; theText )
                );

        //Continue parsing
        text.explode ( theDelimiter; remainingText )
        )
)